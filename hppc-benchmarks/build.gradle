plugins {
  id 'java-library'
  alias(deps.plugins.jmh)
}

dependencies {
  implementation project(":hppc")

  implementation deps.fastutil
  implementation deps.koloboke
  implementation deps.jmh.core

  jmh deps.jmh.core
  jmh deps.jmh.generator.annprocess
}

jmh {
  duplicateClassesStrategy = DuplicatesStrategy.WARN
}

jmhJar {
  duplicatesStrategy = DuplicatesStrategy.WARN
  exclude 'LICENSE'
  exclude 'THIRD-PARTY'
}

task benchmark() {
  dependsOn jmhJar

  doFirst {
    println """JMH benchmarks JAR is at:\n  ${jmhJar.archiveFile.get()}

List benchmarks:
  java -jar ${jmhJar.archiveFile.get()} -l

Run all benchmarks:
  java -jar ${jmhJar.archiveFile.get()}

JMH options:
  java -jar ${jmhJar.archiveFile.get()} -h

Example of filtering to one benchmark and a subset of parameters:
  java -jar ${jmhJar.archiveFile.get()} B002_HashSet_Add -p library=HPPC,FASTUTIL

"""
  }
}