def minJavaVersion = JavaVersion.toVersion(deps.versions.minJava.get())

allprojects {
  plugins.withType(JavaPlugin).configureEach {
    java {
      sourceCompatibility = minJavaVersion
      targetCompatibility = minJavaVersion
    }

    tasks.withType(JavaCompile).configureEach {
      options.encoding = "UTF-8"
      options.compilerArgs += [
        "--release",
        minJavaVersion.majorVersion,
        "-Xlint:unchecked,deprecation",
        "-proc:none",
        "-Werror",
      ]
    }

    tasks.jar {
      manifest {
        attributes([
          "Implementation-Vendor" : "Carrot Search s.c. and contributors",
          "Implementation-Title"  : "${project.name}",
          "Implementation-Version": "${project.version}"
        ])
      }
    }
  }
}